## <summary>User network interface configuration helper</summary>

########################################
## <summary>
##	Execute usernetctl in the usernetctl domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`usernetctl_domtrans',`
	gen_require(`
		type usernetctl_t, usernetctl_exec_t;
	')

	domtrans_pattern($1, usernetctl_exec_t, usernetctl_t)
')

########################################
## <summary>
##	Execute usernetctl in the usernetctl domain, and
##	allow the specified role the usernetctl domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed the usernetctl domain.
##	</summary>
## </param>
## <rolecap/>
#
interface(`usernetctl_run',`
	gen_require(`
		type usernetctl_t;
	')

	usernetctl_domtrans($1)
	role $2 types usernetctl_t;

	sysnet_run_ifconfig(usernetctl_t, $2)
	sysnet_run_dhcpc(usernetctl_t, $2)

	optional_policy(`
		consoletype_run(usernetctl_t, $2)
	')

	optional_policy(`
		iptables_run(usernetctl_t, $2)
	')

	optional_policy(`
		modutils_run_insmod(usernetctl_t, $2)
	')

	optional_policy(`
		ppp_run(usernetctl_t, $2)
	')
')
