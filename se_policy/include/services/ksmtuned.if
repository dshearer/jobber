## <summary>Kernel Samepage Merging (KSM) Tuning Daemon</summary>

########################################
## <summary>
##	Execute a domain transition to run ksmtuned.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`ksmtuned_domtrans',`
	gen_require(`
		type ksmtuned_t, ksmtuned_exec_t;
	')

	domtrans_pattern($1, ksmtuned_exec_t, ksmtuned_t)
')

########################################
## <summary>
##	Execute ksmtuned server in the ksmtuned domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`ksmtuned_initrc_domtrans',`
	gen_require(`
		type ksmtuned_initrc_exec_t;
	')

	init_labeled_script_domtrans($1, ksmtuned_initrc_exec_t)
')

########################################
## <summary>
##	All of the rules required to administrate
##	an ksmtuned environment
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`ksmtuned_admin',`
	gen_require(`
		type ksmtuned_t, ksmtuned_var_run_t;
		type ksmtuned_initrc_exec_t;
	')

	allow $1 ksmtuned_t:process { ptrace signal_perms getattr };
    read_files_pattern($1, ksmtuned_t, ksmtuned_t)

	files_list_pids($1)
	admin_pattern($1, ksmtuned_var_run_t)

	# Allow ksmtuned_t to restart the apache service
	ksmtuned_initrc_domtrans($1)
	domain_system_change_exemption($1)
	role_transition $2 ksmtuned_initrc_exec_t system_r;
	allow $2 system_r;

')
