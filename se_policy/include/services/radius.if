## <summary>RADIUS authentication and accounting server.</summary>

########################################
## <summary>
##	Use radius over a UDP connection.  (Deprecated)
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`radius_use',`
	refpolicywarn(`$0($*) has been deprecated.')
')

########################################
## <summary>
##	All of the rules required to administrate 
##	an radius environment
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`radius_admin',`
	gen_require(`
		type radiusd_t, radiusd_etc_t, radiusd_log_t;
		type radiusd_etc_rw_t, radiusd_var_lib_t, radiusd_var_run_t;
		type radiusd_initrc_exec_t;
	')

	allow $1 radiusd_t:process { ptrace signal_perms };
	ps_process_pattern($1, radiusd_t)

	init_labeled_script_domtrans($1, radiusd_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 radiusd_initrc_exec_t system_r;
	allow $2 system_r;

	files_list_etc($1)
	admin_pattern($1, radiusd_etc_t)

	logging_list_logs($1)
	admin_pattern($1, radiusd_log_t)

	admin_pattern($1, radiusd_etc_rw_t)

	files_list_var_lib($1)
	admin_pattern($1, radiusd_var_lib_t)

	files_list_pids($1)
	admin_pattern($1, radiusd_var_run_t)
')
