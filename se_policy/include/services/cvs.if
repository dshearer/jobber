## <summary>Concurrent versions system</summary>

########################################
## <summary>
##	Read the CVS data and metadata.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`cvs_read_data',`
	gen_require(`
		type cvs_data_t;
	')

	list_dirs_pattern($1, cvs_data_t, cvs_data_t)
	read_files_pattern($1, cvs_data_t, cvs_data_t)
	read_lnk_files_pattern($1, cvs_data_t, cvs_data_t)
')

########################################
## <summary>
##	Allow the specified domain to execute cvs
##	in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`cvs_exec',`
	gen_require(`
		type cvs_exec_t;
	')

	can_exec($1, cvs_exec_t)
')

#####################################
## <summary>
##  Dontaudit Attempts to list the CVS data and metadata.
## </summary>
## <param name="domain">
##  <summary>
##  Domain to not audit.
##  </summary>
## </param>
#
interface(`cvs_dontaudit_list_data',`
    gen_require(`
        type cvs_data_t;
    ')

    dontaudit $1 cvs_data_t:dir list_dir_perms;
')

########################################
## <summary>
##	All of the rules required to administrate 
##	an cvs environment
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed to manage the cvs domain.
##	</summary>
## </param>
## <rolecap/>
#
interface(`cvs_admin',`
	gen_require(`
		type cvs_t, cvs_tmp_t;
		type cvs_data_t, cvs_var_run_t;
		type cvs_initrc_exec_t;
	')

	allow $1 cvs_t:process { ptrace signal_perms };
	ps_process_pattern($1, cvs_t)

	# Allow cvs_t to restart the apache service
	init_labeled_script_domtrans($1, cvs_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 cvs_initrc_exec_t system_r;
	allow $2 system_r;

	files_list_tmp($1)
	admin_pattern($1, cvs_tmp_t)

	admin_pattern($1, cvs_data_t)

	files_list_pids($1)
	admin_pattern($1, cvs_var_run_t)
')
